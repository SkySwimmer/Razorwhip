plugins {
    id 'java'
	id 'maven-publish'
}

version = "1.0.0.A1"
group = "org.asf.razorwhip.sentinel.launcher"

sourceCompatibility = '1.17'
targetCompatibility = '1.17'

def authorname = "AerialWorks Software Foundation"

apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
	maven { name = "AerialWorks"; url = "https://aerialworks.ddns.net/maven" }
}

jar {
	manifest {
		attributes([
			"Main-Class": "org.asf.razorwhip.sentinel.launcher.updater.LauncherUpdaterMain",
			"Specification-Title": "Sentinel",
			"Specification-Vendor": "${authorname}",
            "Specification-Version": project.version,
			"Implementation-Title": project.name,
			"Implementation-Version": project.version,
			"Implementation-Vendor" :"${authorname}",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
	includeEmptyDirs = false
}

task javaDocs(type: Javadoc) {
	classpath = sourceSets.main.runtimeClasspath
	source = sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: 'javaDocs') {
	from javadoc
	classifier = 'javadoc'
	exclude "**/extra/**"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
	exclude "**/extra/**"
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

dependencies {
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
}

project.configurations.implementation.canBeResolved = true

task installation () {
	doLast {
		copy {
			from "launcher.sh"
			from "launcher.bat"
			into "build/Installations"
		}
		for (ResolvedArtifact arti : project.configurations.implementation.resolvedConfiguration.getResolvedArtifacts()) {
			if (arti.getClassifier() != null)
				continue;
			copy {
				from arti.file
				rename arti.file.getName(), arti.name + (arti.getClassifier() != null && !arti.getClassifier().isEmpty() ? "-" + arti.getClassifier() : "") + "." + arti.getExtension()
				into "build/Installations/libs"
			}
		}
		for (def task : project.tasks) {
			if (task instanceof AbstractArchiveTask) {
				if (!task.getArchiveClassifier().get().isEmpty())
					continue;
					
				copy {
					from task.getArchiveFile().get()
					rename task.getArchiveFile().get().getAsFile().getName(), task.getArchiveBaseName().get() + (task.getArchiveClassifier().get() != null && !task.getArchiveClassifier().get().isEmpty() ? "-" + task.getArchiveClassifier().get() : "") + "." + task.getArchiveExtension().get()
					into "build/Installations"
				}
			}
		}
	}
}

installation.dependsOn project.tasks.build
build.finalizedBy installation
